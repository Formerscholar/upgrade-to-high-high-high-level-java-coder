package rules.test.lof.alone

import com.czdx.parkingcharge.domain.pr.ParkingRecord
import com.czdx.parkingcharge.utils.ParkingFeeUtil
import java.math.BigDecimal


// 超出最高限价时间
rule "MY411NCFT-divide-maxcharge"
    activation-group "charge-rule"
    salience 200
    when
        $pRecord:ParkingRecord($duration: parkingDuration >= 2880)
    then
        System.out.println("触发规则：超出最高限价时间分割");
        BigDecimal fee = ParkingFeeUtil.divideCP2ChargeFee($pRecord, 2880, BigDecimal.valueOf(200), "1", "1");
        $pRecord.setParkingFee(fee);
end

// 期间强制分割
rule "MY411NCFT-divide-duration"
    activation-group "charge-rule"
    salience 120
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 120)
    then
        // 按期间时长分割，计算费用
        System.out.println("触发规则：期间强制分割");
        // 分割后，需要根据两者分割情况做可用/不可用处理
        BigDecimal fee = ParkingFeeUtil.divideDuration2ChargeFee($pRecord, 120, "1", "1", BigDecimal.valueOf(200));
        $pRecord.setParkingFee(fee);
end

// 算费不包含免费时间处理，需要扣减掉免费停车时间
rule "MY411NCFT-hide-ncft"
    activation-group "charge-rule"
    salience 110
    when
        $pRecord:ParkingRecord(useFreeTime)
    then
        // 扣减免费时间
        BigDecimal fee = ParkingFeeUtil.substractFreeTime($pRecord, 18, BigDecimal.valueOf(200));
        $pRecord.setParkingFee(fee);
end

// 超过时长后，隐藏的分割，不会考虑免费时长次数、首时段计费方式
rule "MY411NCFT-hide-1"
    activation-group "charge-rule"
    salience 110
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 120, useFirstPeriod)
    then
        // 该分割，不允许使用免费时长、首时段
        BigDecimal fee = ParkingFeeUtil.divideHideDuration2ChargeFeeOnce($pRecord, 120, BigDecimal.valueOf(200));
        $pRecord.setParkingFee(fee);
end

// 超过时长后，隐藏的分割，不会考虑免费时长次数、首时段计费方式
rule "MY411NCFT-hide-2"
    activation-group "charge-rule"
    salience 110
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 90, !useFirstPeriod)
    then
        // 该分割，不允许使用免费时长、首时段
        BigDecimal fee = ParkingFeeUtil.divideHideDuration2ChargeFee($pRecord, 90, BigDecimal.valueOf(200));
        $pRecord.setParkingFee(fee);
end

// 首时段，时长30分钟，5元每30分钟
rule "MY411NCFT-2"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 0, parkingDuration <= 30, useFirstPeriod)
    then
        System.out.println("触发规则：MY411NCFT-2");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee($duration - 0, 30, BigDecimal.valueOf(5), "1", BigDecimal.valueOf(25));
        $pRecord.setParkingFee(fee);
end

// 第二时段，时长30分钟，4元每30分钟 - 可用首时段
rule "MY411NCFT-3"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 30, parkingDuration <= 60, useFirstPeriod)
    then
        System.out.println("触发规则：MY411NCFT-3");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 30, 30, BigDecimal.valueOf(4), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(5));
        $pRecord.setParkingFee(fee);
end

// 第二时段，时长30分钟，4元每30分钟 - 不可用首时段
rule "MY411NCFT-4"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 0, parkingDuration <= 30, !useFirstPeriod)
    then
        System.out.println("触发规则：MY411NCFT-4");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 0, 30, BigDecimal.valueOf(4), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(0));
        $pRecord.setParkingFee(fee);
end

// 第三时段，时长60分钟，3元每60分钟 - 可用首时段
rule "MY411NCFT-5"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 60, useFirstPeriod)
    then
        System.out.println("触发规则：MY411NCFT-5");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 60, 60, BigDecimal.valueOf(3), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(9));
        $pRecord.setParkingFee(fee);
end

// 第三时段，时长60分钟，3元每60分钟 - 不可用首时段
rule "MY411NCFT-6"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 30, !useFirstPeriod)
    then
        System.out.println("触发规则：MY411NCFT-6");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 30, 60, BigDecimal.valueOf(3), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(4));
        $pRecord.setParkingFee(fee);
end
