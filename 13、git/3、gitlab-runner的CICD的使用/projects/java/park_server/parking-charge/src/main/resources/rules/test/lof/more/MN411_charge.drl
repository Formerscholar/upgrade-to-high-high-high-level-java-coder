package rules.test.lof.alone

import com.czdx.parkingcharge.domain.pr.ParkingRecord
import com.czdx.parkingcharge.utils.ParkingFeeUtil
import java.math.BigDecimal


// 期间强制分割
rule "MN411-divide-duration"
    activation-group "charge-rule"
    salience 120
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 120)
    then
        // 按期间时长分割，计算费用
        System.out.println("触发规则：期间强制分割");
        // 分割后，需要根据两者分割情况做可用/不可用处理
        BigDecimal fee = ParkingFeeUtil.divideDuration2ChargeFee($pRecord, 120, "1", "1", BigDecimal.valueOf(0));
        $pRecord.setParkingFee(fee);
end

// 超过时长后，隐藏的分割，不会考虑免费时长次数、首时段计费方式
rule "MN411-hide-1"
    activation-group "charge-rule"
    salience 110
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 120, useFirstPeriod)
    then
        // 该分割，不允许使用免费时长、首时段
        BigDecimal fee = ParkingFeeUtil.divideHideDuration2ChargeFeeOnce($pRecord, 120, BigDecimal.valueOf(0));
        $pRecord.setParkingFee(fee);
end

// 超过时长后，隐藏的分割，不会考虑免费时长次数、首时段计费方式
rule "MN411-hide-2"
    activation-group "charge-rule"
    salience 110
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 90, !useFirstPeriod)
    then
        // 该分割，不允许使用免费时长、首时段
        BigDecimal fee = ParkingFeeUtil.divideHideDuration2ChargeFee($pRecord, 90, BigDecimal.valueOf(0));
        $pRecord.setParkingFee(fee);
end

// 免费时间段 18 分钟
rule "MN411-1"
    activation-group "charge-rule"
    salience 100
    when
        $pRecord:ParkingRecord($duration: parkingDuration <= 18, useFreeTime)
    then
        System.out.println("触发规则：MN411-1");
        $pRecord.setParkingFee(BigDecimal.ZERO);
end

// 首时段，时长30分钟，5元每30分钟
rule "MN411-2"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 0, parkingDuration <= 30, useFirstPeriod)
    then
        System.out.println("触发规则：MN411-2");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee($duration - 0, 30, BigDecimal.valueOf(5), "1", BigDecimal.valueOf(50));
        $pRecord.setParkingFee(fee);
end

// 第二时段，时长30分钟，4元每30分钟 - 可用首时段
rule "MN411-3"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 30, parkingDuration <= 60, useFirstPeriod)
    then
        System.out.println("触发规则：MN411-3");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 30, 30, BigDecimal.valueOf(4), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(5));
        $pRecord.setParkingFee(fee);
end

// 第二时段，时长30分钟，4元每30分钟 - 不可用首时段
rule "MN411-4"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 0, parkingDuration <= 30, !useFirstPeriod)
    then
        System.out.println("触发规则：MN411-4");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 0, 30, BigDecimal.valueOf(4), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(0));
        $pRecord.setParkingFee(fee);
end

// 第三时段，时长60分钟，3元每60分钟 - 可用首时段
rule "MN411-5"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 60, useFirstPeriod)
    then
        System.out.println("触发规则：MN411-5");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 60, 60, BigDecimal.valueOf(3), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(9));
        $pRecord.setParkingFee(fee);
end

// 第三时段，时长60分钟，3元每60分钟 - 不可用首时段
rule "MN411-6"
    activation-group "charge-rule"
    salience 90
    when
        $pRecord:ParkingRecord($duration: parkingDuration > 30, !useFirstPeriod)
    then
        System.out.println("触发规则：MN411-6");
        BigDecimal fee = ParkingFeeUtil.chargeParkingFee(
                $duration - 30, 60, BigDecimal.valueOf(3), "1", BigDecimal.valueOf(25), BigDecimal.valueOf(4));
        $pRecord.setParkingFee(fee);
end
